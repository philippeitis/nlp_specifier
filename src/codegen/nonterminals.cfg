# Specification root
S -> MRET | RETIF | SPEC_ATOM | SPEC_COND

## Wrapping default POS tokens for more general parsing
# Noun with arbitrarily many adjectives
MNN -> NN | NNS | NNP | NNPS | JJ MNN | VBN MNN
# Adjective with optional determinant
TJJ -> DT_Q JJ | DT_Q JJR | DT_Q JJS
# Adjective with modifier
MJJ -> RB_Q JJ | RB_Q JJR | RB_Q JJS
# Verb with modifier
MVB -> RB_Q VB | RB_Q VBZ | RB_Q VBP | RB_Q VBN | RB_Q VBG | RB_Q VBD
# If and only if
IFF -> IF CC RB IF
# than or equal to (eg. greater EQTO)
EQTO -> IN CC JJ IN

## Operations
# Modified boolean operation (bitwise/logical  and / or)
BITOP -> JJ CC | NN CC
# Arithmetic operation
ARITHOP -> ARITH IN_Q
# Shift operation
SHIFTOP -> SHIFT IN DT NN IN | JJ SHIFT
# Any operation
OP -> BITOP | ARITHOP | SHIFTOP

# Objects
OBJ -> PRP | DT_Q MNN | CODE | DT_Q LIT | OBJ OP OBJ | STR | CHAR | DT VBG MNN | PROP_OF OBJ

## Object relationships
# Relationship to object
# less than object, less than or equal to object, is object, is less than a or b
REL -> TJJ IN OBJ | TJJ EQTO OBJ | IN OBJ | REL CC OBJ
# Modified relationship
# not less than object
MREL -> RB_Q REL
# is adjective, is relation, is a literal (eg. ==)
# eg. (Object) is something, (Object) is less than something, (Object) occurred
PROP -> MVB MJJ | MVB MREL | MVB OBJ | MVB | MVB RANGEMOD
# A property of an item
# A value of
PROP_OF -> DT MNN IN DT | DT MNN IN | DT MJJ IN | DT MJJ IN DT

## Ranges
# Range separator, between range start and range end
# between a AND b, from a TO b
RSEP -> CC | IN | TO
# Range
# OBJECT from START to END, from START to END, up to END
RANGE -> OBJ IN OBJ RSEP OBJ | IN OBJ RSEP OBJ | IN IN OBJ
# Range with or without modifier - eg. exclusive or inclusive of range end
RANGEMOD -> RANGE | RANGE COMMA_Q JJ

# Object is something, object 1 and object 2 are something
ASSERT -> OBJ PROP | OBJ CC ASSERT
# Object must be something, object 1 and object 2 must be something
HASSERT -> OBJ MD PROP | OBJ CC HASSERT
# For some object
QUANT -> FOR DT OBJ
# Quantifier, or quantifier with range, or quantifier with additional modifiers
# For all objects in range, For all objects, For all objects in range and with some property
QUANT_EXPR -> QUANT RANGEMOD_Q | QUANT_EXPR COMMA_Q CC MREL | QUANT_EXPR COMMA_Q MREL
# Quantifier, with assertion over items
# For all items in range, some expression (must/will)? be true
QASSERT -> QUANT_EXPR COMMA_Q ASSERT | ASSERT QUANT_EXPR | CODE QUANT_EXPR
HQASSERT -> QUANT_EXPR COMMA_Q HASSERT | HASSERT QUANT_EXPR | CODE QUANT_EXPR

## Return operations
# Return object
MRET -> RET OBJ | OBJ VBZ RET | OBJ RET
# Items which represent boolean condition
BOOL_EXPR -> ASSERT | QASSERT | CODE | EVENT
# If or IFF boolean is true
COND -> IF BOOL_EXPR | IFF BOOL_EXPR
# Return obj if condition is satisfied
# Return object if condition
RETIF -> MRET COND | COND COMMA MRET | RETIF COMMA RB RETIF | RETIF COMMA RB OBJ | MRET COMMA MRET COND


## Side effects
#       object is verbed to object | object verbed | object is verbed | obj is right shifted by n | Prints {some item} from source (and returns)
SIDE -> OBJ VBZ MVB IN OBJ | OBJ VBZ MVB | OBJ VBZ MJJ MVB IN OBJ | VBZ TJJ OBJ IN OBJ CC MRET
ASSIGN -> VBZ OBJ IN OBJ | VBZ OBJ TO OBJ | VBZ DT_Q JJ_Q OBJ

# eg. event_type occurred
# Overlaps with ASSERT: OBJ / PROP (MNN/MVB)
# in turn, overlaps with ASSERT,
# Appears in BOOL_EXPR, then COND, then RETIF
EVENT -> MNN VBD | MNN VBZ

# X (must)? be true if condition, otherwise returns `false`
SPEC_ATOM -> HASSERT | HQASSERT | SIDE | ASSIGN
SPEC_COND -> SPEC_ATOM COND | COND COMMA_Q SPEC_ATOM
# Specialization of SPEC_ATOM, used specifically to allow implicit trailing return statement
# RETIF     -> MRET      COND | COND COMMA_Q MRET
# `x` if cond | if cond, `x`| if cond, `x`, otherwise, RECURSE
# SPEC_ITEM -> SPEC_ITEM | RETIF | SPEC_ITEM COMMA_Q RB SPEC_ITEM
# Terminal specification
# SPEC_TERM -> SPEC_ITEM COMMA_Q RB RETIF_TERM
# (something is true) if condition, otherwise (something else is true)
# SPEC_TERM -> SPEC_ITEM COMMA_Q RB SPEC_ATOM
# SPEC_TERM -> SPEC_ITEM COMMA_Q RB MRET
# Trailing checks - eg. returning x if overflow occurs
# SPEC_TERM -> SPEC_ITEM COMMA_Q MRET COND
# SPEC_TERM -> SPEC_ITEM COMMA_Q SPEC_ATOM COND
# Singletons
# SPEC_TERM -> MRET | RETIF | SPEC_ATOM | RETIF_TERM
# Terminal RETIF, allows implicit return
# RETIF_TERM -> RETIF COMMA_Q RB OBJ
